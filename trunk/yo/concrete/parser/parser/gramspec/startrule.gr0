    lines:
        lines
        line
    |
        line
    ;

    line:
        intExpr
        '\n'
        {
            cerr << "RPN: " << d_rpn.str() << endl;
            cerr << "int: " << $1 << endl;
            d_rpn.str(string());
        }
    |
        doubleExpr
        '\n'
        {
            cerr << "RPN: " << d_rpn.str() << endl;
            cerr << "double: " << $1 << endl;
            d_rpn.str(string());
        }
    |
        '\n'
        {
            cout << "Good bye\n";
            YYACCEPT;
        }
    |
        error
        '\n'
        {
            d_rpn.clear();
            d_rpn.str(string());
        }
    ;

    intExpr:
        intExpr '*' intExpr
        {
            d_rpn << " * ";
            $$ = $1 * $3;
        }
    |
        intExpr '+' intExpr
        {
            d_rpn << " + ";
            $$ = $1 + $3;
        }
    |
        '(' intExpr ')'
        {
            $$ = $2;
        }
    |
        '-' intExpr         %prec UnaryMinus
        {
            d_rpn << " n ";
            $$ = -$2;
        }
    |
        INT
        {
            $$ = A2x(d_scanner.YYText());
            d_rpn << " " << $$ << " ";
        }
    ;

    doubleExpr:
        doubleExpr '*' doubleExpr
        {
            d_rpn << " * ";
            $$ = $1 * $3;
        }
    |
        doubleExpr '+' doubleExpr
        {
            d_rpn << " + ";
            $$ = $1 + $3;
        }
    |
        doubleExpr '*' intExpr
        {
            d_rpn << " * ";
            $$ = $1 * $3;
        }
    |
        doubleExpr '+' intExpr
        {
            d_rpn << " + ";
            $$ = $1 + $3;
        }
    |
        intExpr '*' doubleExpr
        {
            d_rpn << " * ";
            $$ = $1 * $3;
        }
    |
        intExpr '+' doubleExpr
        {
            d_rpn << " + ";
            $$ = $1 + $3;
        }
    |
        '(' doubleExpr ')'
        {
            $$ = $2;
        }
    |
        '-' doubleExpr         %prec UnaryMinus
        {
            d_rpn << " n ";
            $$ = -$2;
        }
    |
        DOUBLE
        {
            $$ = A2x(d_scanner.YYText());
            d_rpn << " " << $$ << " ";
        }
    ;
